dnl
dnl xfce4-notes-plugin - Notes plugin for the Xfce4 panel
dnl
dnl 2003 Jakob Henriksson <b0kaj+dev@lysator.liu.se>
dnl 2006-2012 Mike Massonnet <mmassonnet@gmail.com>
dnl

m4_define([xfce4_notes_plugin_version_major], [1])
m4_define([xfce4_notes_plugin_version_minor], [8])
m4_define([xfce4_notes_plugin_version_micro], [1])
m4_define([xfce4_notes_plugin_version_nano], [])
m4_define([xfce4_notes_plugin_version_build], [@REVISION@])
m4_define([xfce4_notes_plugin_version_tag], []) # Leave empty for releases
m4_define([xfce4_notes_plugin_version], [xfce4_notes_plugin_version_major().xfce4_notes_plugin_version_minor().xfce4_notes_plugin_version_micro()ifelse(xfce4_notes_plugin_version_nano(), [], [], [.xfce4_notes_plugin_version_nano()])ifelse(xfce4_notes_plugin_version_tag(), [git], [xfce4_notes_plugin_version_tag()-xfce4_notes_plugin_version_build()], [xfce4_notes_plugin_version_tag()])])

AC_PREREQ([2.50])
AC_INIT([Notes], [xfce4_notes_plugin_version], [http://bugzilla.xfce.org/], [xfce4-notes-plugin],
	[http://goodies.xfce.org/projects/panel-plugins/xfce4-notes-plugin])
AC_REVISION([xfce4_notes_plugin_version_build])
AC_CANONICAL_TARGET()
AC_CONFIG_HEADERS([config.h])

LT_PREREQ([2.2.6])
LT_INIT([disable-static])

AM_INIT_AUTOMAKE([1.13 dist-bzip2 no-dist-gzip])
AM_MAINTAINER_MODE()
AM_SILENT_RULES([yes])

dnl Check for basic programs
AM_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_INSTALL()

dnl Check for standard header files
AC_HEADER_STDC()

dnl Check for Math lib useful for lib/color.c
AC_CHECK_HEADERS([math.h])
AC_SUBST([MATH_LIBS], [" -lm"])

dnl Check for required packages
XDT_CHECK_LIBX11_REQUIRE()
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.24.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.10.0])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.10.0])

dnl Check for GTK
AC_ARG_ENABLE([gtk3],
              [AC_HELP_STRING([--enable-gtk3], [Build with GTK3])],
              [enable_gtk3=$enableval], [enable_gtk3=no])
if test "x$enable_gtk3" = "xyes"; then
        dnl GIO 2.28 for GApplication
        XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.28.0])
        XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.2.0])
	XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.10.0])
        AC_DEFINE([ENABLE_GTK3], [1], [Defined if build with GTK3 is enabled])
else
        XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.24.0])
        XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.20.0])
	XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-1], [4.10.0])
        XDT_CHECK_PACKAGE([UNIQUE], [unique-1.0], [1.0.0])
fi
AM_CONDITIONAL([ENABLE_GTK3], [test "x$enable_gtk3" = "xyes"])

dnl Check for Xfce Panel
if test "x$enable_gtk3" = "xno" ; then
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.10.0])
fi

dnl Check for valac
if test "x$USE_MAINTAINER_MODE" = "xyes" ; then
        AM_PROG_VALAC([0.16.0])
        if test "x$VALAC" = "x" ; then
                AC_MSG_ERROR([Cannot find the "valac" compiler in your PATH])
        fi
        XDT_CHECK_PACKAGE([XFCE4VALA], [xfce4-vala], [4.10])
fi

dnl Translations
IT_PROG_INTLTOOL()
XDT_I18N([@LINGUAS@])

dnl Check for debugging support
XDT_FEATURE_DEBUG()

AC_OUTPUT([
Makefile
data/Makefile
data/gtk-2.0/Makefile
data/gtk-2.0/tabs/Makefile
data/gtk-2.0/scrollbars/Makefile
data/icons/Makefile
data/icons/16x16/Makefile
data/icons/22x22/Makefile
data/icons/24x24/Makefile
data/icons/32x32/Makefile
data/icons/scalable/Makefile
data/pixmaps/Makefile
lib/Makefile
src/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Maintainer Mode:        $USE_MAINTAINER_MODE"
if test "x$USE_MAINTAINER_MODE" = "xyes" ; then
echo
echo "    * Vala:               $VALAC $am__vala_version"
if test "x$enable_gtk3" = "xno" ; then
echo "    * Xfce4-vala:         $XFCE4VALA_VERSION"
fi
echo
fi
echo "* GTK+:                   $GTK_VERSION"
if test "x$enable_gtk3" = "xyes" ; then
echo "* W"
echo "* W GTK3 support is only experimental, don't use for production."
echo "* W"
echo
fi
if test "x$enable_gtk3" = "xno" ; then
echo "* Xfce Panel:             $LIBXFCE4PANEL_VERSION"
fi
echo "* Debug Support:          $enable_debug"
echo

