dnl
dnl xfce4-notes-plugin - Notes plugin for the Xfce4 panel
dnl
dnl 2003 Jakob Henriksson <b0kaj+dev@lysator.liu.se>
dnl 2006-2010 Mike Massonnet <mmassonnet@gmail.com>
dnl

m4_define([xfce4_notes_plugin_version_major], [1])
m4_define([xfce4_notes_plugin_version_minor], [7])
m4_define([xfce4_notes_plugin_version_micro], [7])
m4_define([xfce4_notes_plugin_version_nano], [])
m4_define([xfce4_notes_plugin_version_build], [@REVISION@])
m4_define([xfce4_notes_plugin_version_tag], []) # Leave empty for releases
m4_define([xfce4_notes_plugin_version], [xfce4_notes_plugin_version_major().xfce4_notes_plugin_version_minor().xfce4_notes_plugin_version_micro()ifelse(xfce4_notes_plugin_version_nano(), [], [], [.xfce4_notes_plugin_version_nano()])ifelse(xfce4_notes_plugin_version_tag(), [git], [xfce4_notes_plugin_version_tag()-xfce4_notes_plugin_version_build()], [xfce4_notes_plugin_version_tag()])])

AC_PREREQ([2.50])
AC_INIT([Notes], [xfce4_notes_plugin_version], [http://bugzilla.xfce.org/], [xfce4-notes-plugin],
	[http://goodies.xfce.org/projects/panel-plugins/xfce4-notes-plugin])
AC_REVISION([xfce4_notes_plugin_version_build])
AC_CANONICAL_TARGET()

AM_INIT_AUTOMAKE([1.11 dist-bzip2])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
AM_SILENT_RULES([yes])

dnl Avoid g77 and g++ checks
m4_undefine([AC_PROG_CXX])
m4_defun([AC_PROG_CXX],[])
m4_undefine([AC_PROG_F77])
m4_defun([AC_PROG_F77],[])

dnl Check for basic programs
AM_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_LIBTOOL()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()

dnl Check for standard header files
AC_HEADER_STDC()

dnl Check for Math lib useful for lib/color.c
AC_CHECK_HEADERS([math.h])
AC_SUBST([MATH_LIBS], [" -lm"])

dnl Check for required packages
XDT_CHECK_LIBX11_REQUIRE()
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.16.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.14.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.4.0])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.6.0])
XDT_CHECK_PACKAGE([UNIQUE], [unique-1.0], [1.0.0])

dnl Check for libxfce4panel >= 4.7
XDT_CHECK_OPTIONAL_PACKAGE([LIBXFCE4PANEL47], [libxfce4panel-1.0], [4.7.0], [], [])

dnl Check for valac
if test "x$USE_MAINTAINER_MODE" = "xyes" ; then
	AM_PROG_VALAC([0.10.0])
	if test "x$VALAC" = "x" ; then
		AC_MSG_ERROR([Cannot find the "valac" compiler in your PATH])
	fi
	if test "x$LIBXFCE4PANEL47_FOUND" = "xyes" ; then
		XDT_CHECK_PACKAGE([XFCE4VALA], [xfce4-vala], [4.7])
	else
		XDT_CHECK_PACKAGE([XFCE4VALA], [xfce4-vala], [4.6])
		PKG_CHECK_EXISTS([xfce4-vala >= 4.7],
				AC_ERROR([
	*** Build configured for Xfce Panel 4.6 but  ***
	*** Xfce Vala bindings for 4.7 are installed ***]), [])
	fi
fi

dnl Translations
XDT_I18N([@LINGUAS@])

dnl Check for debugging support
XDT_FEATURE_DEBUG()

AC_OUTPUT([
Makefile
data/Makefile
data/gtk-2.0/Makefile
data/gtk-2.0/scrollbars/Makefile
data/icons/Makefile
data/icons/16x16/Makefile
data/icons/22x22/Makefile
data/icons/24x24/Makefile
data/icons/32x32/Makefile
data/icons/scalable/Makefile
data/pixmaps/Makefile
lib/Makefile
src/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Maintainer Mode:        $USE_MAINTAINER_MODE"
if test "x$USE_MAINTAINER_MODE" = "xyes" ; then
echo
echo "    * Vala:               $VALAC $am__vala_version"
echo
fi
echo "* GTK+:                   $GTK_VERSION"
echo "* Xfce Panel:             $LIBXFCE4PANEL_VERSION"
echo "* Debug Support:          $enable_debug"
echo

