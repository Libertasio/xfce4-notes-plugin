dnl
dnl xfce4-notes-plugin - Notes plugin for the Xfce4 panel
dnl
dnl 2003 Benedikt Meurer <benedikt.meurer@unix-ag.uni-siegen.de>
dnl 2003 Jakob Henriksson <b0kaj+dev@lysator.liu.se>
dnl 2006, 2008, 2009 Mike Massonnet <mmassonnet@gmail.com>
dnl

m4_define([xfce4_notes_plugin_version_major], [1])
m4_define([xfce4_notes_plugin_version_minor], [7])
m4_define([xfce4_notes_plugin_version_micro], [1])
m4_define([xfce4_notes_plugin_version_nano], [])
m4_define([xfce4_notes_plugin_version_build], [r@REVISION@])
m4_define([xfce4_notes_plugin_version_tag], [svn]) # Leave empty for releases
m4_define([xfce4_notes_plugin_version], [xfce4_notes_plugin_version_major().xfce4_notes_plugin_version_minor().xfce4_notes_plugin_version_micro()ifelse(xfce4_notes_plugin_version_nano(), [], [], [.xfce4_notes_plugin_version_nano()])ifelse(xfce4_notes_plugin_version_tag(), [svn], [xfce4_notes_plugin_version_tag()-xfce4_notes_plugin_version_build()], [xfce4_notes_plugin_version_tag()])])

AC_PREREQ([2.50])
AC_INIT([xfce4-notes-plugin], [xfce4_notes_plugin_version], 
        [mmassonnet@xfce.org])
AC_REVISION([project_version_build])
AC_CANONICAL_TARGET()

AM_INIT_AUTOMAKE([1.8 dist-bzip2])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

dnl Avoid g77 and g++ checks
m4_undefine([AC_PROG_CXX])
m4_defun([AC_PROG_CXX],[])
m4_undefine([AC_PROG_F77])
m4_defun([AC_PROG_F77],[])

dnl Check for basic programs
AC_PROG_CC_C_O
AC_PROG_LD
AC_PROG_INSTALL
AC_PROG_LIBTOOL
AC_PROG_INTLTOOL

dnl Check for standard header files
AC_HEADER_STDC
AC_CHECK_HEADERS([math.h])

dnl Check for X11 installed
XDT_CHECK_LIBX11_REQUIRE

dnl Check for required packages
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.10.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.4.0])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.6.0])

dnl Check for libxfce4panel >= 4.7
XDT_CHECK_OPTIONAL_PACKAGE([LIBXFCE4PANEL47], [libxfce4panel-1.0], [4.7.0], [], [])

dnl Check for valac
valac_minimum_version=0.7.1
AC_PATH_PROG([VALAC], [valac], [valac])
AC_SUBST(VALAC)
if test "x$USE_MAINTAINER_MODE" = "xyes" ; then
	if test "xHAVE_LIBXFCE4PANEL47" = "xyes" ; then
		XDT_CHECK_PACKAGE([XFCE4VALA], [xfce4-vala], [0.1])
	else
		XDT_CHECK_PACKAGE([XFCE4VALA], [xfce4-vala], [0.1]) # 0.2
	fi
fi

dnl Translations
XDT_I18N([@LINGUAS@])

dnl Check for debugging support
XDT_FEATURE_DEBUG()

AC_OUTPUT([
Makefile
icons/Makefile
icons/16x16/Makefile
icons/22x22/Makefile
icons/24x24/Makefile
icons/32x32/Makefile
icons/scalable/Makefile
panel-plugin/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Maintainer Mode:        $USE_MAINTAINER_MODE"
if test "x$USE_MAINTAINER_MODE" = "xyes" ; then
echo "    * Vala:               $has_valac"
echo "      You need Vala $valac_minimum_version or higher (run $VALAC --version)"
echo
fi
echo "* Debug Support:          $enable_debug"
echo

